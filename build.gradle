buildscript {
    repositories {
        mavenLocal()
        jcenter()
    }

    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.0'
        classpath "me.champeau.gradle:jmh-gradle-plugin:0.2.0"
    }

}

def GROUP_ID = "com.fullcontact.marshal"
def VERSION = "1.4.0"

configure(subprojects) {
    apply from:"file:../cobertura.gradle"
}

ext {
    jmhLibraryVersion = '1.11.1'
}

// common repositories
allprojects {
    repositories {
        // local repository
        // NOTE: gradle has issues with mavenLocal and mavenCentral being enabled at the same time
        //mavenLocal()

        // main repository
        mavenCentral()

        maven {
            name "Artifactory Realm"
            url "http://fullcontact.artifactoryonline.com/fullcontact/repo/"
            credentials {
                username = "deployer"
                password = "***REMOVED***"
            }
        }
    }
}

subprojects {
    apply plugin: "base"
    apply plugin: "maven"
    apply plugin: "java"

    dependencies {
        testCompile group: "junit", name: "junit", version: "4.8.2"
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                if (VERSION.endsWith("-SNAPSHOT")) {
                    repository(url: "https://fullcontact.artifactoryonline.com/fullcontact/libs-snapshots-local") {
                        authentication(userName: "deployer", password: "***REMOVED***")
                    }
                } else {
                    repository(url: "https://fullcontact.artifactoryonline.com/fullcontact/libs-releases-local") {
                        authentication(userName: "deployer", password: "***REMOVED***")
                    }
                }
            }
        }
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = "sources"
        from sourceSets.main.allSource
    }
    artifacts {
        archives sourcesJar
    }

    compileJava.options.compilerArgs = [ "-Xlint:unchecked", "-Xlint:deprecation" ]
    compileTestJava.options.compilerArgs = [ "-Xlint:unchecked", "-Xlint:deprecation" ]
    sourceCompatibility = "1.6"
    targetCompatibility = "1.6"

    test {
        jvmArgs "-agentlib:hprof=cpu=samples,interval=2,depth=30,thread=y,file=build/profile.hprof"
    }

    group = GROUP_ID
    version = VERSION
}

project(":marshal") {
    apply plugin: 'me.champeau.gradle.jmh'
    apply plugin: 'com.github.johnrengelman.shadow' // jmh

    jmh {
        jmhVersion = jmhLibraryVersion
        iterations = 10
        warmupIterations = 10
        fork = 2
        resultsFile = project.file("${project.buildDir}/reports/jmh/results.csv")
        jvmArgs "-agentlib:hprof=cpu=samples,interval=2,depth=30,thread=y,file=build/profile.hprof"
    }

    dependencies {
        compile group: "com.google.guava", name:"guava", version:"15.0"
        testCompile "org.openjdk.jmh:jmh-core:$jmhLibraryVersion"
        testCompile "org.openjdk.jmh:jmh-generator-annprocess:$jmhLibraryVersion"
    }
}

project(":marshal-mapreduce") {
    repositories {
        maven {
            name "Cloudera"
            url "https://repository.cloudera.com/artifactory/cloudera-repos"
        }
    }

    dependencies {
        compile project(":marshal")
        compile group: "com.google.guava", name:"guava", version: "15.0"
        compile group: "org.apache.hadoop", name: "hadoop-client", version: "2.6.0-mr1-cdh5.4.0"
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.8'
}
